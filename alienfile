use alienfile;

plugin 'PkgConfig' => 'openssl';

share {
  start_url 'https://ftp.openbsd.org/pub/OpenBSD/LibreSSL/';
  plugin Download => (
    version       => qr/^libressl-([0-9\.]+)\.tar\.gz$/,
    bootstrap_ssl => 1,
  );

  plugin Extract => 'tar.gz';

  meta->prop->{out_of_source} = 1;

  if($^O eq 'MSWin32')
  {
    plugin 'Build::CMake';

    if(meta->prop->{platform}->{compiler_type} eq 'microsoft')
    {
      requires 'Text::Patch';
      requires 'Path::Tiny';

      meta->after_hook(
        extract => sub {
          my($build) = @_;
          my($dir) = map { $_->basename } Path::Tiny->new('.')->children;
          chdir $dir;
          $build->system('%{perl} %{.install.patch}/libressl-2.6.2.pl');
          die 'execute failed' if $?;
          chdir '..';
        }
      );

      gather sub {
        my($build) = @_;
        my $prefix = $build->runtime_prop->{prefix};

	my @libs = grep /^[a-z]+\.lib$/, map { $_->basename } Path::Tiny->new('.')->child('lib')->children;
        
        $build->runtime_prop->{$_} = "-I$prefix/include " for qw( cflags cflags_static );
        $build->runtime_prop->{$_} = "-LIBPATH:$prefix/lib @libs " for qw( libs libs_static );
      };
    }
  }
  else
  {
    plugin 'Build::Autoconf';
  }
}

